TxtListParser {
    file = < item*:xs >:r !. -> Node("Document" r "" xs)
    item =
        <
        '* '
        restLine:x
        indentedLine*:xs
        >:r
        -> Node("Item" r "" [x ~xs])
    restLine = < (!'\n' .)*:xs lineEnd >:r -> Node("Line" r { xs })
    lineEnd =
        | '\n' -> "\n"
        | !. -> ""
    indentedLine =
        | < '\n' >:r -> Node("Line" r "")
        | '  ' restLine
}

TxtListPrettyPrinter {
    pretty = %:x !.  -> x
    prettyInner = [%:x !.] -> x
    Document = . firstItem:x restItem*:ys -> { x ys }
    firstItem = prettyInner
    restItem = prettyInner
    Item = . firstLine:x restLine*:ys -> { x ys }
    Line = .:value -> { value "\n" }
    firstLine = prettyInner:x -> { "* " x }
    restLine =
        | [['Line'!.] [!.]] -> "\n"
        | prettyInner:x -> { "  " x }
}

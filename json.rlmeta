JsonParser {
    file      = < document:x s >:r !.         -> Node("Document" r "" [x])
    document  =
        | string
        | true
        | false
        | null
        | number
        | list
        | dict
    string    = s < '"' char*:xs '"' >:r      -> Node("String" r {xs})
    true      = s < 'true' >:r                -> Node("True" r "")
    false     = s < 'false' >:r               -> Node("False" r "")
    null      = s < 'null' >:r                -> Node("Null" r "")
    number    = s < digit:x digit*:xs >:r     -> Node("Number" r int({[x ~xs]}))
    digit     = '0'-'9'
    list      = s < '[' items:x s ']' >:r     -> Node("List" r "" x)
    items     =
        | document:x (s ',' document)*:xs       -> [x ~xs]
        |                                       -> []
    dict      = s < '{' dictItems:x s '}' >:r -> Node("Dict" r "" x)
    dictItems =
        | dictItem:x (s ',' dictItem)*:xs       -> [x ~xs]
        |                                       -> []
    dictItem  = s < key:x s ':' document:y >:r  -> Node("Entry" r "" [x y])
    key       = < '"' char*:xs '"' >:r      -> Node("Key" r {xs})
    char      = !'"' .
    s         = (' ' | '\n')*
}

JsonPrettyPrinter {
    pretty = %:x !.  -> { x "\n" }
    prettyInner = [%:x !.] -> x
    prettyInnerItems =
        | prettyInner:x (prettyInner:y -> {",\n" y})*:xs -> {
                                                      "\n"
                                                      indent({x xs})
                                                      "\n"
                                                  }
        | -> []
    Document = . prettyInner
    Dict   = . prettyInnerItems:x !.       -> { "{" x "}" }
    List   = . prettyInnerItems:x !.       -> { "[" x "]" }
    Entry  = . prettyInner:key prettyInner:value -> { key ": " value }
    Number = .:value                       -> { value }
    True   = .                             -> "true"
    False  = .                             -> "false"
    Null   = .                             -> "null"
    Key    = .:value                       -> {"\"" value "\""}
    String = .:value                       -> {"\"" value "\""}
}

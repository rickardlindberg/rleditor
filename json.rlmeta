JsonParser {
    file      = #:s document:x s #:e !.         -> Node("Document" s e "" [x])
    document  =
        | string
        | true
        | false
        | null
        | number
        | list
        | dict
    string    = s #:s '"' char*:xs '"' #:e      -> Node("String" s e {xs})
    true      = s #:s 'true' #:e                -> Node("True" s e "")
    false     = s #:s 'false' #:e               -> Node("False" s e "")
    null      = s #:s 'null' #:e                -> Node("Null" s e "")
    number    = s #:s digit:x digit*:xs #:e     -> Node("Number" s e int({[x ~xs]}))
    digit     = '0'-'9'
    list      = s #:s '[' items:x s ']' #:e     -> Node("List" s e "" x)
    items     =
        | document:x (s ',' document)*:xs       -> [x ~xs]
        |                                       -> []
    dict      = s #:s '{' dictItems:x s '}' #:e -> Node("Dict" s e "" x)
    dictItems =
        | dictItem:x (s ',' dictItem)*:xs       -> [x ~xs]
        |                                       -> []
    dictItem  = #:s key:x s ':' document:y #:e  -> Node("Entry" s e "" [x y])
    key       = s #:s '"' char*:xs '"' #:e      -> Node("Key" s e {xs})
    char      = !'"' .
    s         = (' ' | '\n')*
}

JsonPrettyPrinter {
    pretty = %:x !.  -> x
    prettyInner = [%:x !.] -> x
    prettyInnerItems =
        | prettyInner:x (prettyInner:y -> {",\n" y})*:xs -> {
                                                      "\n"
                                                      indent({x xs})
                                                      "\n"
                                                  }
        | -> []
    Document = . prettyInner
    Dict   = . prettyInnerItems:x !.       -> { "{" x "}" }
    List   = . prettyInnerItems:x !.       -> { "[" x "]" }
    Entry  = . prettyInner:key prettyInner:value -> { key ": " value }
    Number = .:value                       -> { value }
    True   = .                             -> "true"
    False  = .                             -> "false"
    Null   = .                             -> "null"
    Key    = .:value                       -> {"\"" value "\""}
    String = .:value                       -> {"\"" value "\""}
}
